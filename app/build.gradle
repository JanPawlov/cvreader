import org.ajoberstar.grgit.Grgit

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"
apply plugin: "pl.droidsonroids.localization"
apply from: "$project.rootDir/versions.gradle"
apply from: "$project.rootDir/jacoco.gradle"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://www.jitpack.io"
        }
        jcenter()
    }
    dependencies {
        classpath "org.ajoberstar.grgit:grgit-gradle:3.0.0"
    }
}

def git = Grgit.open(currentDir: projectDir)
def shortenedCommitHash = git.head().getAbbreviatedId()
def commitsOnBranchCount = git.log(includes: ["HEAD"]).size()

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.triive"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode commitsOnBranchCount
        versionName "0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            versionNameSuffix ".$commitsOnBranchCount"
            resValue "string", "app_name", "CVReader"
            manifestPlaceholders = [crashlyticsEnabled: true]
            buildConfigField "boolean", "CRASH_REPORTING_ENABLED", "true"
        }
        qa {
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            applicationIdSuffix ".qa"
            versionNameSuffix ".$shortenedCommitHash.${commitsOnBranchCount}.qa"
            resValue "string", "app_name", "CVReader QA"
            manifestPlaceholders = [crashlyticsEnabled: true]
            buildConfigField "boolean", "CRASH_REPORTING_ENABLED", "true"
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-debug-rules.pro", "proguard-rules.pro"
            applicationIdSuffix ".debug"
            versionNameSuffix ".$shortenedCommitHash.${commitsOnBranchCount}.debug"
            resValue "string", "app_name", "CVReader Debug"
            manifestPlaceholders = [crashlyticsEnabled: false]
            buildConfigField "boolean", "CRASH_REPORTING_ENABLED", "false"
            testCoverageEnabled false
            ext.enableCrashlytics = false
        }
    }
    signingConfigs {
        debug {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("keystore/debug.keystore")
            storePassword "android"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.core:core-ktx:$versions.coreKtx"
    implementation "androidx.fragment:fragment-ktx:$versions.fragmentKtx"
    implementation "com.jakewharton.timber:timber:$versions.timber"
    implementation "com.crashlytics.sdk.android:crashlytics:$versions.crashlytics"
    implementation "org.koin:koin-core:$versions.koin"
    implementation "org.koin:koin-core-ext:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"
    implementation "org.koin:koin-android-ext:$versions.koin"
    implementation "org.koin:koin-androidx-scope:$versions.koin"
    implementation "com.squareup.moshi:moshi:$versions.moshi"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    implementation "com.github.gturedi:stateful-layout:$versions.statefulLayout"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    testImplementation "junit:junit:$versions.junit"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$versions.mockitoKotlin"
    testImplementation "org.mockito:mockito-inline:$versions.mockito"
    testImplementation "com.willowtreeapps.assertk:assertk-jvm:$versions.assertk"
    testImplementation "androidx.arch.core:core-testing:$versions.lifecycle"
    testImplementation "org.koin:koin-test:$versions.koin"

    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
    androidTestImplementation "androidx.test.ext:junit-ktx:$versions.testRunner"
}

localization {
    xlsFileURI = "https://docs.google.com/spreadsheets/d/XXXX/export?format=xlsx"
    handleEmptyTranslationsAsDefault = true
    outputIndent = "    "
    skipInvalidName = true
}
